apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.manimarank.spell4wiki"
        vectorDrawables.useSupportLibrary = true
        minSdkVersion 19
        targetSdkVersion 31
        versionName "1.2.1"
        versionCode 11
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    bundle {
        language {
            // Specifies that the app bundle should not support
            // configuration APKs for language resources. These
            // resources are instead packaged with each base and
            // dynamic feature APK.
            enableSplit = false
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'

    // UI related libraries
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.0.0'
    // implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // Animation libraries
    implementation 'br.com.simplepass:loading-button-android:1.14.0'

    // FFMPEG - Audio wav to ogg converter
    implementation 'com.arthenica:mobile-ffmpeg-audio:4.3.1.LTS' // support api 16+

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    // Networking libraries
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:3.12.8"
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // Coroutines
    def coroutines_version = "1.5.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'

    // Room
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Lottie Animation
    implementation 'com.airbnb.android:lottie:3.4.0'

    // Crash Reporter
    implementation 'com.gitlab.manimaran:crashreporter:v1.0'

    // Show case
    implementation 'uk.co.samuelwall:material-tap-target-prompt:3.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    implementation 'org.jetbrains:annotations:20.1.0'
}

// F-Droid build: Excluded proprietary plugins, sdk libraries
if (!appBuildType.contains(fdroidBuild)) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    dependencies {
        implementation platform('com.google.firebase:firebase-bom:29.0.2')
        implementation 'com.google.firebase:firebase-analytics-ktx'
        implementation 'com.google.firebase:firebase-crashlytics-ktx'
    }
}
